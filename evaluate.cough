{ addEnv }                   = require './env-utilities'
{ car }                      = require './linked-list'
{ cdr }                      = require './linked-list'
commentSignal                = require './commentSignal'
{ createMalIndex }           = require './mal-type-utilities'
{ createMalList }            = require './mal-type-utilities'
{ createMalMacro }           = require './mal-type-utilities'
{ createMalNumber }          = require './mal-type-utilities'
{ createMalString }          = require './mal-type-utilities'
{ createMalSymbol }          = require './mal-type-utilities'
{ createMalUserFunction }    = require './mal-type-utilities'
{ def! }                     = require './keyTokens'
{ _do }                      = require './keyTokens'
{ empty? }                   = require './linked-list' 
{ expand-macro }             = require './keyTokens'
extractJsValue               = require './extractJsValue'
{ fn* }                      = require './keyTokens'
{ forEach }                  = require './linked-list'
{ _if }                      = require './keyTokens'
{ let* }                     = require './keyTokens'
{ lookup }                   = require './env-utilities'
{ macro* }                   = require './keyTokens'
{ malCoreFunction? }         = require './mal-type-utilities'
{ malIndex? }                = require './mal-type-utilities'
{ malList? }                 = require './mal-type-utilities'
{ malMacro? }                = require './mal-type-utilities'
{ malNil }                   = require './mal-type-utilities'
{ malSymbol? }               = require './mal-type-utilities'
{ malUserFunction? }         = require './mal-type-utilities'
{ map }                      = require './linked-list'
{ next }                     = require './linked-list'
{ quasiquote }               = require './keyTokens'
{ quote }                    = require './keyTokens'
{ spliceUnquote }            = require './keyTokens'
{ unquote }                  = require './keyTokens'
{ recurse }                  = require './linked-list'
{ reduce }                   = require './linked-list'
{ reduceBy2 }                = require './linked-list'
{ reverse }                  = require './linked-list'
{ setMainEnv }               = require './env-utilities'
{ toPartialArray }           = require './linked-list'
{ undef! }                   = require './keyTokens'
{ unsetMainEnv }             = require './env-utilities'

createFn = (malList, envs) ->
  createMalUserFunction
    localEnvs     : (envs.slice 0)
    malExpression : (next malList)
    malParameters : (car malList)

SPLAT-TOKEN = '&'
createLocalEnv = (jsParams, malArgs) ->
  env = {}
  until empty? jsParams
    jsParam = car jsParams
    if jsParam == SPLAT-TOKEN
      env[next jsParams] = malArgs
      return env
    else
      env[jsParam] = car malArgs
      jsParams = cdr jsParams
      malArgs = cdr malArgs
  env

createMacro = (malList, envs) ->
  createMalMacro
    localEnvs     : (envs.slice 0)
    malExpression : (next malList)
    malParameters : (car malList)

defineNewValue = (malList, envs) ->
  jsKey = extractJsValue (car malList)
  malValue = _evaluate ((next malList), envs)
  setMainEnv (envs, jsKey, malValue)

undefineValue = (malList, envs) ->
  jsKey = extractJsValue (car malList)
  unsetMainEnv (envs, jsKey)

evalAllSideEffects = (malList, envs) ->
  forEach ((evaluate envs), malList)

evalCondition = (malList, envs) ->
  jsBool = extractJsValue (_evaluate ((car malList), envs))
  malExpr = if jsBool then next malList else next (cdr malList)
  _evaluate (malExpr, envs)

evalDefault = (malExpr, envs) ->
  malExpr

evalMultiCondition = (malList, envs) ->
  until empty? malList
    condition = car malList
    jsBool = extractJsValue (_evaluate ((car condition), envs))
    return _evaluate ((next condition), envs) if jsBool
    malList = cdr malList
  malNil

_normalizeQuasiquote = \malExpr ->
  if unquotedExpr? malExpr
    next malExpr
  else if spliceUnquotedExpr? malExpr
    concat ((car malExpr), (normalizeQuasiquote (cdr malExpr)))
  else if malList? malExpr
    map (normalizeQuasiquote, malExpr)
  else
    malExpr

normalizeQuasiquote = \malExpr ->
  if not malList? malExpr
    return doQuote malExpr
  if empty? malExpr
    return doQuote malExpr
  _normalizeQuasiquote malExpr

evalQuasiquotedExpr = (expr, envs) ->
  _expr = car expr
  res = switch
    when unquotedExpr? _expr
      _evaluate ((next _expr), envs)
    when malList? _expr
      _evalQuasiquotedExpr (_expr, envs)
    else
      _expr
  res

_evalQuasiquotedExpr = (expr, envs) ->
  manageItem = (memo, item) ->
    switch
      when unquotedExpr? item
        createMalList(_evaluate(next(item), envs), memo)
      when spliceUnquotedExpr? item
        _manageItem = (_memo, _item) ->
          createMalList (_item, _memo)
        reduce (memo, _manageItem, (_evaluate ((next item), envs)))
      when malList? item
        createMalList (_evalQuasiquotedExpr (item, envs), memo)
      else
        createMalList (item, memo)
  reverse(
    reduce (
      createMalList ()
      manageItem
      expr))

evalQuote = (malList, envs) ->
  car malList

evalSymbol = (malSymbol, envs) ->
  lookup (envs, (extractJsValue malSymbol))

_evaluate = (malExpr, envs) ->
  loop
    switch
      when malList? malExpr
        [head, a1, remaining] = toPartialArray (2, malExpr)
        t1 = cdr malExpr

        switch (extractJsValue head)
          when def!
            return defineNewValue (t1, envs) 

          when undef!
            return undefineValue (t1, envs) 

          when let*
            _reduce = (env, malKey, malExpr) ->
              envValue = _evaluate (malExpr, envs)
              jsKey = extractJsValue malKey
              env[jsKey] = envValue
              env
            malExpr = (car remaining)
            envs = addEnv (envs, reduceBy2 ({}, _reduce, a1))

          # How optimize tail calls?
          when _do
            return evalAllSideEffects (t1, envs)

          when _if
            jsBool = extractJsValue (_evaluate (a1, envs))
            malExpr = if jsBool then (car remaining) else (next remaining)

          when fn*
            return createFn (t1, envs)

          when macro*
            return createMacro (t1, envs)

          when quote
            return evalQuote (t1, envs)

          when quasiquote
            return evalQuasiquotedExpr (t1, envs)

          when expand-macro
            return expandMacro ((car a1), (cdr a1), envs)

          else
            malSymbol = head
            malExpr = t1

            malInvokable = _evaluate (malSymbol, envs)

            switch
              when malMacro? malInvokable
                malExpr = expandMacro (head, t1, envs)

              when malCoreFunction? malInvokable
                { fn, fnName } = extractJsValue malInvokable
                malArgs = map ((evaluate envs), malExpr)
                return fn malArgs

              when malUserFunction? malInvokable
                { localEnvs, malExpression, malParameters } =
                  extractJsValue malInvokable

                malArgs = map ((evaluate envs), malExpr)
                malExpr = malExpression
                jsParameters = map (extractJsValue, malParameters)
                newEnv = createLocalEnv (jsParameters, malArgs)
                envs = addEnv (localEnvs, newEnv)

              else
                throw 'Value is not a function'

      when malSymbol? malExpr
        return evalSymbol (malExpr, envs)

      # NOTE: Happens over and over for every malIndex.
      when malIndex? malExpr
        index = extractJsValue malExpr
        newIndex = {}
        for own key, value of index
          newIndex[key] = _evaluate (index[key], envs)
        return createMalIndex newIndex

      else
        return evalDefault (malExpr, envs)

evaluate = \envs \malExpr ->
  return commentSignal if malExpr == commentSignal
  _evaluate (malExpr, envs)

expandMacro = (malMacroSymbol, malArgs, envs) ->
  malMacro = _evaluate (malMacroSymbol, envs)
  { localEnvs, malExpression, malParameters } = extractJsValue malMacro
  jsParameters = map (extractJsValue, malParameters)
  newEnv = createLocalEnv (jsParameters, malArgs)
  newEnvStack = addEnv (localEnvs, newEnv)
  _evaluate (malExpression, newEnvStack)

spliceUnquote? = \malValue ->
  spliceUnquote == (extractJsValue malValue)

spliceUnquotedExpr? = \malValue ->
  malList? malValue && (spliceUnquote? (car malValue))

unquote? = \malValue ->
  unquote == (extractJsValue malValue)

unquotedExpr? = \malValue ->
  malList? malValue && (unquote? (car malValue))

module.exports = { evaluate, expandMacro }
